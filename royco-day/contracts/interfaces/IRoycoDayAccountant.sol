// SPDX-License-Identifier: LicenseRef-PolyForm-Perimeter-1.0.1
pragma solidity ^0.8.28;

import { MarketState, Operation, SyncedAccountingState } from "../libraries/Types.sol";
import { NAV_UNIT } from "../libraries/Units.sol";

/// @title IRoycoDayAccountant
/// @notice Interface for the RoycoDayAccountant contract that manages accounting, coverage, and liquidity requirements for a Royco market
interface IRoycoDayAccountant {
    /**
     * @notice Initialization parameters for the Royco Accountant
     * @custom:field kernel - The kernel that this accountant maintains mark-to-market NAV, JT impermanent loss, and fee accounting for
     * @custom:field initialAuthority - The initial authority for the accountant
     * @custom:field fixedTermGracePeriodSeconds - The seconds after deployment during which the market cannot enter a fixed term no matter what, so a young market is never locked by an early junior impermanent loss
     * @custom:field minCoverageWAD - The coverage ratio that the senior tranche is expected to be protected by, scaled to WAD precision
     * @custom:field coverageLiquidationUtilizationWAD - The liquidation coverageUtilization threshold for this market, scaled to WAD precision
     * @custom:field minLiquidityWAD - The percentage of the senior tranche NAV that must be in the liquidity provider tranche's market making inventory, scaled to WAD precision
     * @custom:field jtYDM - The junior tranche's Yield Distribution Model (JT YDM), responsible for determining the yield share (risk premium) payed from the senior tranche yield to the junior tranche
     * @custom:field jtYDMInitializationData - The data used to initialize the JT YDM for this market
     * @custom:field lptYDM - The liquidity provider tranche's Yield Distribution Model (LPT YDM), responsible for determining the yield share (liquidity premium) payed from the senior tranche yield to the liquidity provider tranche
     * @custom:field lptYDMInitializationData - The data used to initialize the LPT YDM for this market
     * @custom:field maxJTYieldShareWAD - The maximum JT yield share (risk premium) as a percentage of senior appreciation, scaled to WAD precision
     * @custom:field maxLPTYieldShareWAD - The maximum LPT yield share (liquidity premium) as a percentage of senior appreciation, scaled to WAD precision
     * @custom:field fixedTermDurationSeconds - The duration of a fixed term for this market in seconds
     * @custom:field dustTolerance - The worst case dust tolerance for collateralNAV from underlying NAV quoting/rounding
     * @custom:field stProtocolFeeWAD - The market's configured protocol fee percentage taken from yield earned by the senior tranche, scaled to WAD precision
     * @custom:field jtProtocolFeeWAD - The market's configured protocol fee percentage taken from yield earned by the junior tranche, scaled to WAD precision
     * @custom:field jtYieldShareProtocolFeeWAD - The market's configured protocol fee percentage taken from the yield share (risk premium) payed from the senior tranche yield to the junior tranche, scaled to WAD precision
     * @custom:field lptYieldShareProtocolFeeWAD - The market's configured protocol fee percentage taken from the yield share (liquidity premium) payed from the senior tranche yield to the liquidity provider tranche, scaled to WAD precision
     */
    struct RoycoDayAccountantInitParams {
        // Market Contracts
        address kernel;
        address initialAuthority;
        // Deployment Configuration
        uint24 fixedTermGracePeriodSeconds;
        // Coverage configuration
        uint64 minCoverageWAD;
        uint256 coverageLiquidationUtilizationWAD;
        // Liquidity configuration
        uint64 minLiquidityWAD;
        // Yield Distribution Models
        address jtYDM;
        bytes jtYDMInitializationData;
        address lptYDM;
        bytes lptYDMInitializationData;
        // Maximum yield shares (premiums)
        uint64 maxJTYieldShareWAD;
        uint64 maxLPTYieldShareWAD;
        // Fixed term duration
        uint24 fixedTermDurationSeconds;
        // Dust tolerance
        NAV_UNIT dustTolerance;
        // Protocol fees
        uint64 stProtocolFeeWAD;
        uint64 jtProtocolFeeWAD;
        uint64 jtYieldShareProtocolFeeWAD;
        uint64 lptYieldShareProtocolFeeWAD;
    }

    /**
     * @notice Storage state for the Royco Accountant
     * @custom:storage-location erc7201:Royco.storage.RoycoDayAccountantState
     * @custom:field stProtocolFeeWAD - The market's configured protocol fee percentage charged from yield earned by the senior tranche, scaled to WAD precision
     * @custom:field jtProtocolFeeWAD - The market's configured protocol fee percentage charged from yield earned by the junior tranche, scaled to WAD precision
     * @custom:field jtYieldShareProtocolFeeWAD - The market's configured protocol fee percentage charged from the yield share (risk premium) payed from the senior tranche yield to the junior tranche, scaled to WAD precision
     * @custom:field lptYieldShareProtocolFeeWAD - The market's configured protocol fee percentage charged from the yield share (liquidity premium) payed from the senior tranche yield to the liquidity provider tranche, scaled to WAD precision
     * @custom:field minCoverageWAD - The coverage percentage that the senior tranche is expected to be protected by, scaled to WAD precision
     * @custom:field minLiquidityWAD - The percentage of the senior tranche NAV that must be in the liquidity provider tranche's market making inventory, scaled to WAD precision
     * @custom:field fixedTermDurationSeconds - The duration of a fixed term for this market in seconds
     * @custom:field lastMarketState - The last recorded state of this market (perpetual or fixed term)
     * @custom:field fixedTermEndTimestamp - The end timestamp of the currently ongoing fixed term (set to 0 if the market is in a perpetual state)
     * @custom:field lastYieldShareAccrualTimestamp - The timestamp at which the time-weighted yield share accumulators were last updated
     * @custom:field lastPremiumPaymentTimestamp - The timestamp at which the last premium payments occurred (the risk and liquidity premiums are always paid together)
     * @custom:field jtYDM - The junior tranche's Yield Distribution Model (JT YDM), responsible for determining the yield share (risk premium) payed from the senior tranche yield to the junior tranche
     * @custom:field maxJTYieldShareWAD - The maximum JT yield share (risk premium) as a percentage of senior appreciation, scaled to WAD precision
     * @custom:field lptYDM - The liquidity provider tranche's Yield Distribution Model (LPT YDM), responsible for determining the yield share (liquidity premium) payed from the senior tranche yield to the liquidity provider tranche
     * @custom:field maxLPTYieldShareWAD - The maximum LPT yield share (liquidity premium) as a percentage of senior appreciation, scaled to WAD precision
     * @custom:field twJTYieldShareAccruedWAD - The time-weighted junior tranche yield share (JT YDM output) since the last premium payment, scaled to WAD precision
     * @custom:field twLPTYieldShareAccruedWAD - The time-weighted liquidity provider tranche yield share (LPT YDM output) since the last premium payment, scaled to WAD precision
     * @custom:field fixedTermCommenceableAtTimestamp - The timestamp at which the market can enter a fixed term, the deployment time plus the fixed-term grace period
     * @custom:field fixedTermEndTimestamp - The end timestamp of the currently ongoing fixed term (set to 0 if the market is in a perpetual state)
     * @custom:field coverageLiquidationUtilizationWAD - The liquidation coverageUtilization threshold for this market, scaled to WAD precision
     * @custom:field lastCollateralNAV - The last recorded pure value of the coinvested collateral backing the senior and junior tranches
     * @custom:field lastSTEffectiveNAV - The last recorded effective NAV (including any prior applied coverage, ST yield distribution, and uncovered losses) of the senior tranche
     * @custom:field lastJTEffectiveNAV - The last recorded effective NAV (including any prior provided coverage, JT yield, ST yield distribution, and JT losses) of the junior tranche
     * @custom:field lastJTImpermanentLoss - The junior tranche's impermanent loss: JT's recoverable drawdown, deepened by JT losses and provided coverage, repaid by JT gains and JT's first claim on ST appreciation
     * @custom:field lastLPTRawNAV - The last recorded raw NAV of the liquidity provider tranche: the mark-to-market value of its invested assets
     * @custom:field dustTolerance - The worst case dust tolerance for collateralNAV from underlying NAV quoting/rounding, effective NAV deltas are pro-rata attributions of the collateral NAV delta so it bounds their dust too
     */
    struct RoycoDayAccountantState {
        // Slot 0
        uint64 stProtocolFeeWAD;
        uint64 jtProtocolFeeWAD;
        uint64 jtYieldShareProtocolFeeWAD;
        uint64 lptYieldShareProtocolFeeWAD;
        // Slot 1
        uint64 minCoverageWAD;
        uint64 minLiquidityWAD;
        uint24 fixedTermDurationSeconds;
        MarketState lastMarketState;
        uint32 fixedTermEndTimestamp;
        uint32 lastYieldShareAccrualTimestamp;
        uint32 lastPremiumPaymentTimestamp;
        // Slot 2
        address jtYDM;
        uint64 maxJTYieldShareWAD;
        // Slot 3
        address lptYDM;
        uint64 maxLPTYieldShareWAD;
        // Slot 4 (uint128 holds over 1e13 years of the config-capped WAD-per-second accrual)
        uint128 twJTYieldShareAccruedWAD;
        uint128 twLPTYieldShareAccruedWAD;
        // Slot 5
        address kernel;
        uint64 fixedTermCommenceableAtTimestamp;
        // Slot 6-11
        uint256 coverageLiquidationUtilizationWAD;
        NAV_UNIT lastCollateralNAV;
        NAV_UNIT lastSTEffectiveNAV;
        NAV_UNIT lastJTEffectiveNAV;
        NAV_UNIT lastJTImpermanentLoss;
        NAV_UNIT lastLPTRawNAV;
        NAV_UNIT dustTolerance;
    }

    /**
     * @notice Emitted when the JT and LPT shares of ST yield (the risk and liquidity premiums) are accrued based on the market's coverageUtilization and liquidityUtilization since the last accrual
     * @param jtYieldShareWAD JT's instantaneous yield share (JT YDM output) based on coverageUtilization since the last accrual
     * @param twJTYieldShareAccruedWAD The time-weighted JT yield share accrued since the last yield distribution
     * @param lptYieldShareWAD LPT's instantaneous yield share (LPT YDM output) based on liquidityUtilization since the last accrual
     * @param twLPTYieldShareAccruedWAD The time-weighted LPT yield share accrued since the last liquidity premium payment
     */
    event YieldSharesAccrued(uint256 jtYieldShareWAD, uint256 twJTYieldShareAccruedWAD, uint256 lptYieldShareWAD, uint256 twLPTYieldShareAccruedWAD);

    /// @notice Emitted when a fixed term regime is commenced by this market
    /// @param fixedTermEndTimestamp The end timestamp of the new fixed term regime
    event FixedTermCommenced(uint32 fixedTermEndTimestamp);

    /// @notice Emitted when the junior tranche yield distribution model is updated
    /// @param jtYDM The new junior tranche's YDM address
    event JuniorTrancheYDMUpdated(address jtYDM);

    /// @notice Emitted when the senior tranche protocol fee percentage is updated
    /// @param stProtocolFeeWAD The new protocol fee percentage charged on senior tranche yield, scaled to WAD precision
    event SeniorTrancheProtocolFeeUpdated(uint64 stProtocolFeeWAD);

    /// @notice Emitted when the junior tranche protocol fee percentage is updated
    /// @param jtProtocolFeeWAD The new protocol fee percentage charged on junior tranche yield, scaled to WAD precision
    event JuniorTrancheProtocolFeeUpdated(uint64 jtProtocolFeeWAD);

    /// @notice Emitted when the junior tranche yield share (risk premium) protocol fee percentage is updated
    /// @param jtYieldShareProtocolFeeWAD The new protocol fee percentage charged from the yield share (risk premium) payed from the senior tranche yield to the junior tranche, scaled to WAD precision
    event JuniorTrancheYieldShareProtocolFeeUpdated(uint64 jtYieldShareProtocolFeeWAD);

    /// @notice Emitted when the coverage percentage requirement is updated
    /// @param minCoverageWAD The new coverage percentage, scaled to WAD precision
    event MinCoverageUpdated(uint64 minCoverageWAD);

    /// @notice Emitted when the liquidation threshold parameter is updated
    /// @param liquidationCoverageUtilizationWAD The new liquidation coverageUtilization threshold for this market, scaled to WAD precision
    event LiquidationCoverageUtilizationUpdated(uint256 liquidationCoverageUtilizationWAD);

    /// @notice Emitted when the fixed term duration is updated
    /// @param fixedTermDurationSeconds The new fixed term duration for this market in seconds
    event FixedTermDurationUpdated(uint24 fixedTermDurationSeconds);

    /// @notice Emitted when the dust tolerance is updated
    /// @param dustTolerance The dust tolerance in NAV units to account for minuscule deltas in the collateral's underlying NAV calculations
    event DustToleranceUpdated(NAV_UNIT dustTolerance);

    /// @notice Emitted when JT's coverage loss is realized and reset to zero when transitioning from a fixed term state to a perpetual state
    /// @param jtImpermanentLossErased The amount of JT impermanent loss erased, effectively marking the JT losses as realized
    event JuniorTrancheImpermanentLossReset(NAV_UNIT jtImpermanentLossErased);

    /// @notice Emitted when a fixed term regime is ended by this market
    event FixedTermEnded();

    /// @notice Emitted when the LPT YDM (liquidity provider tranche Yield Distribution Model) address is updated
    /// @param lptYDM The new LPT YDM address
    event LiquidityProviderTrancheYDMUpdated(address lptYDM);

    /// @notice Emitted when the yield share (liquidity premium) protocol fee percentage is updated
    /// @param lptYieldShareProtocolFeeWAD The new protocol fee percentage charged from the yield share (liquidity premium) payed from the senior tranche yield to the liquidity provider tranche, scaled to WAD precision
    event LiquidityProviderTrancheYieldShareProtocolFeeUpdated(uint64 lptYieldShareProtocolFeeWAD);

    /// @notice Emitted when the liquidity percentage requirement is updated
    /// @param minLiquidityWAD The new percentage of the senior tranche NAV that must be in the liquidity provider tranche's market making inventory, scaled to WAD precision
    event MinLiquidityUpdated(uint64 minLiquidityWAD);

    /// @notice Emitted when the timestamp at which the market can first enter a fixed term is set
    /// @param fixedTermCommenceableAtTimestamp The timestamp at which the market can first enter a fixed term
    event FixedTermCommenceableAt(uint64 fixedTermCommenceableAtTimestamp);

    /**
     * @notice Emitted when the maximum JT and LPT yield shares (premiums) are updated
     * @param maxJTYieldShareWAD The new maximum JT yield share (risk premium) as a percentage of senior appreciation, scaled to WAD precision
     * @param maxLPTYieldShareWAD The new maximum LPT yield share (liquidity premium) as a percentage of senior appreciation, scaled to WAD precision
     */
    event MaxYieldSharesUpdated(uint64 maxJTYieldShareWAD, uint64 maxLPTYieldShareWAD);

    /// @notice Thrown when the caller of the function is not the accountant's configured Royco Kernel
    error ONLY_ROYCO_KERNEL();

    /// @notice Thrown when the accountant's coverage configuration is invalid (can be due to incorrect coverage or liquidation coverageUtilization values)
    error INVALID_COVERAGE_CONFIG();

    /// @notice Thrown when the accountant's liquidity configuration is invalid (the minimum liquidity must be less than 100%)
    error INVALID_LIQUIDITY_CONFIG();

    /// @notice Thrown when the accountant's yield share configuration is invalid (the maximum JT and LPT yield shares must sum to at most 100%)
    error INVALID_MAX_YIELD_SHARE_CONFIG();

    /// @notice Thrown when the configured protocol fee exceeds the maximum
    error MAX_PROTOCOL_FEE_EXCEEDED();

    /// @notice Thrown when the junior and liquidity provider tranche YDMs are identical
    error YDMS_CANNOT_BE_IDENTICAL();

    /// @notice Thrown when the collateral NAV doesn't equal the sum of the effective NAVs of both tranches
    error NAV_CONSERVATION_VIOLATION();

    /// @notice Thrown when the combined risk and liquidity premiums exceed the senior gain they are drawn from: the JT and LPT yield shares must sum to at most 100% of senior appreciation
    error PREMIUMS_EXCEED_SENIOR_YIELD();

    /// @notice Thrown when the operation and NAVs passed to post-op lead to an invalid state
    error INVALID_POST_OP_STATE(Operation _op);

    /**
     * @notice Synchronizes the effective NAVs and impermanent losses of both tranches by marking them to market
     * @dev Must be called before any NAV mutating operation
     * @dev Accrues the JT and LPT yield shares over time based on the market's JT and LPT YDM outputs
     * @dev Persists updated NAV and impermanent loss checkpoints for the next sync to use as reference
     * @dev The returned state's lptRawNAV and liquidityUtilizationWAD are zero placeholders: this sync does not mark the liquidity
     *      tranche
     *      The kernel commits the freshly marked liquidity provider tranche raw NAV via commitLiquidityProviderTrancheRawNAV after minting the
     *      fee shares, then refreshes both fields in the state packet in memory
     * @param _collateralNAV The current pure value of the coinvested collateral backing the senior and junior tranches
     * @return state The synced NAV, impermanent loss, and fee accounting containing all mark-to-market accounting data
     */
    function preOpSyncTrancheAccounting(NAV_UNIT _collateralNAV) external returns (SyncedAccountingState memory state);

    /**
     * @notice Commits the liquidity provider tranche's freshly marked raw NAV
     * @dev MUST be called by the kernel only after preOpSyncTrancheAccounting has committed the senior/junior NAVs AND the resulting
     *      premium and protocol fee shares have been minted, this ordering is required for correctness: the fresh mark is read from the
     *      AMM or another market-making venue oracle, whose senior leg is rate-scaled by the senior share rate (committed senior effective NAV over senior supply), so
     *      only after the pre-op sync and its share mints does the mark reflect the final post-sync, post-mint senior state
     *      Committing it out of this order records a liquidity provider tranche raw NAV against a stale senior state
     * @dev Committing the liquidity provider tranche raw NAV here, separately from the pre-op waterfall, is what keeps it out of the P&L
     *      attribution and out of the senior share rate provider's dependency loop
     *      The kernel derives the resulting liquidity
     *      utilization from this mark and the synced senior effective NAV it already holds, avoiding an extra storage read
     * @param _freshLPTRawNAV The liquidity provider tranche's freshly marked raw NAV (the oracle value of the AMM or another market-making venue)
     */
    function commitLiquidityProviderTrancheRawNAV(NAV_UNIT _freshLPTRawNAV) external;

    /**
     * @notice Previews a synchronization of the effective NAVs and impermanent losses of both tranches by marking them to market
     * @dev The returned state's lptRawNAV and liquidityUtilizationWAD are zero placeholders (this sync does not mark the liquidity provider tranche), the kernel preview refreshes them in memory
     * @param _collateralNAV The current pure value of the coinvested collateral backing the senior and junior tranches
     * @return state The synced NAV, impermanent loss, and fee accounting containing all mark-to-market accounting data
     */
    function previewSyncTrancheAccounting(NAV_UNIT _collateralNAV) external view returns (SyncedAccountingState memory state);

    /**
     * @notice Applies the post-operation (deposit or redemption) collateral NAV delta to the effective NAV checkpoints and commits the liquidity provider tranche's fresh raw NAV
     * @dev Strictly interprets the collateral NAV delta as a deposit/redemption instead of PNL
     * @dev Unlike the pre-op sync, the post-op sync runs no waterfall and pays no premium, so it can commit the liquidity provider tranche raw NAV
     *      directly (the kernel marks it after the operation's pool mutation has settled) and report the resulting liquidity utilization
     * @dev The market's coverage and liquidity requirements are enforced by the kernel's post-op sync against the returned state
     * @param _op The operation being executed in between the pre and post operation synchronizations
     * @param _collateralNAV The post-op pure value of the coinvested collateral backing the senior and junior tranches
     * @param _lptRawNAV The post-op liquidity provider tranche's freshly marked raw NAV (the oracle value of the AMM or another market-making venue), committed by this call
     * @param _stSelfLiquidationBonusNAV The self-liquidation bonus remitted to an ST LP on redemption after the liquidation coverageUtilization threshold has been breached, sourced from JT effective NAV
     * @return state The synced NAV, impermanent loss, and fee accounting containing all mark-to-market accounting data
     */
    function postOpSyncTrancheAccounting(
        Operation _op,
        NAV_UNIT _collateralNAV,
        NAV_UNIT _lptRawNAV,
        NAV_UNIT _stSelfLiquidationBonusNAV
    )
        external
        returns (SyncedAccountingState memory state);

    /**
     * @notice Returns the maximum assets depositable into the senior tranche without violating the market's coverage or liquidity requirements
     * @dev Always rounds in favor of senior tranche protection
     * @param state The synced accounting state that the maximum senior deposit is computed against
     * @return maxDepositableNAV The maximum assets depositable into the senior tranche without violating the market's coverage or liquidity requirements
     */
    function maxSTDeposit(SyncedAccountingState memory state) external view returns (NAV_UNIT maxDepositableNAV);

    /**
     * @notice Returns the maximum assets withdrawable from the junior tranche without violating the market's coverage requirement
     * @dev Always rounds in favor of senior tranche protection
     * @param state The synced accounting state that the maximum junior withdrawal is computed against
     * @return jtWithdrawableNAV The maximum assets that the junior tranche can withdraw, denominated in NAV units
     */
    function maxJTWithdrawal(SyncedAccountingState memory state) external view returns (NAV_UNIT jtWithdrawableNAV);

    /**
     * @notice Returns the maximum assets withdrawable from the liquidity provider tranche without violating the market's liquidity requirement
     * @dev Always rounds in favor of senior tranche protection
     * @param state The synced accounting state that the maximum liquidity withdrawal is computed against
     * @return lptWithdrawableNAV The maximum market-making depth that the liquidity provider tranche can withdraw, denominated in NAV units
     */
    function maxLPTWithdrawal(SyncedAccountingState memory state) external view returns (NAV_UNIT lptWithdrawableNAV);

    /**
     * @notice Updates the JT YDM (Junior Tranche Yield Distribution Model) for this market
     * @dev Only callable by a designated admin
     * @param _jtYDM The new JT YDM address to set
     * @param _jtYDMInitializationData The data used to initialize the new JT YDM for this market
     */
    function setJuniorTrancheYDM(address _jtYDM, bytes calldata _jtYDMInitializationData) external;

    /**
     * @notice Updates the LPT YDM (Liquidity Provider Tranche Yield Distribution Model) for this market
     * @dev Only callable by a designated admin
     * @param _lptYDM The new LPT YDM address to set
     * @param _lptYDMInitializationData The data used to initialize the new LPT YDM for this market
     */
    function setLiquidityProviderTrancheYDM(address _lptYDM, bytes calldata _lptYDMInitializationData) external;

    /**
     * @notice Updates the senior tranche protocol fee percentage for this market
     * @dev Only callable by a designated admin
     * @param _stProtocolFeeWAD The new protocol fee percentage charged on senior tranche yield, scaled to WAD precision
     */
    function setSeniorTrancheProtocolFee(uint64 _stProtocolFeeWAD) external;

    /**
     * @notice Updates the junior tranche protocol fee percentage for this market
     * @dev Only callable by a designated admin
     * @param _jtProtocolFeeWAD The new protocol fee percentage charged on junior tranche yield, scaled to WAD precision
     */
    function setJuniorTrancheProtocolFee(uint64 _jtProtocolFeeWAD) external;

    /**
     * @notice Updates the yield share (risk premium) protocol fee percentage for this market
     * @dev Only callable by a designated admin
     * @param _jtYieldShareProtocolFeeWAD The new protocol fee percentage charged on the yield share (risk premium) payed from senior tranche yield to the junior tranche, scaled to WAD precision
     */
    function setJTYieldShareProtocolFee(uint64 _jtYieldShareProtocolFeeWAD) external;

    /**
     * @notice Updates the yield share (liquidity premium) protocol fee percentage for this market
     * @dev Only callable by a designated admin
     * @param _lptYieldShareProtocolFeeWAD The new protocol fee percentage charged on the yield share (liquidity premium) payed from senior tranche yield to the liquidity provider tranche, scaled to WAD precision
     */
    function setLPTYieldShareProtocolFee(uint64 _lptYieldShareProtocolFeeWAD) external;

    /**
     * @notice Updates the minimum coverage requirement for this market
     * @dev Only callable by a designated admin
     * @param _minCoverageWAD The new coverage percentage, scaled to WAD precision
     */
    function setMinCoverage(uint64 _minCoverageWAD) external;

    /**
     * @notice Updates the liquidation coverageUtilization threshold for this market
     * @dev Only callable by a designated admin
     * @param _liquidationCoverageUtilizationWAD The new liquidation coverageUtilization threshold for this market, scaled to WAD precision
     */
    function setLiquidationCoverageUtilization(uint256 _liquidationCoverageUtilizationWAD) external;

    /**
     * @notice Updates the minimum liquidity requirement for this market
     * @dev Only callable by a designated admin
     * @param _minLiquidityWAD The new percentage of the senior tranche NAV that must be in the liquidity provider tranche's market making inventory, scaled to WAD precision
     */
    function setMinLiquidity(uint64 _minLiquidityWAD) external;

    /**
     * @notice Updates the maximum JT and LPT yield shares (premiums) for this market
     * @dev Only callable by a designated admin
     * @param _maxJTYieldShareWAD The new maximum JT yield share (risk premium) as a percentage of senior appreciation, scaled to WAD precision
     * @param _maxLPTYieldShareWAD The new maximum LPT yield share (liquidity premium) as a percentage of senior appreciation, scaled to WAD precision
     */
    function setMaxYieldShares(uint64 _maxJTYieldShareWAD, uint64 _maxLPTYieldShareWAD) external;

    /**
     * @notice Updates the fixed term duration for this market
     * @dev Setting the fixed term duration to 0 will force the market into an eternally perpetual state
     * @dev Only callable by a designated admin
     * @param _fixedTermDurationSeconds The new fixed term duration for this market in seconds
     */
    function setFixedTermDuration(uint24 _fixedTermDurationSeconds) external;

    /**
     * @notice Updates the dust tolerance in NAV units to account for minuscule deltas in the underlying protocol's NAV calculations, due to rounding
     * @dev Can be safely set to 0 if the underlying investments do not exhibit rounding behavior
     * @dev Only callable by a designated admin
     * @param _dustTolerance The collateral NAV tolerance for rounding discrepancies
     */
    function setDustTolerance(NAV_UNIT _dustTolerance) external;

    /// @notice Returns the state of the accountant
    /// @return state The state of the accountant
    function getState() external view returns (RoycoDayAccountantState memory state);
}
